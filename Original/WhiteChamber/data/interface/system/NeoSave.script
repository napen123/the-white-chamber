#include "scripts\base.inc"
#include "scripts\keys.inc"

self.xResult = false;

var NumSlotButtons = 6;
var NumSavegames = 100;
var ScrollOffset = 0;
var SelectedSlot = -1;

SetState();

////////////////////////////////////////////////////////////////////////////////
on "close"
{
  self.Close();
}

////////////////////////////////////////////////////////////////////////////////
on "up"
{
  ScrollOffset = ScrollOffset - 1;
  SetState();
}

////////////////////////////////////////////////////////////////////////////////
on "down"
{
  ScrollOffset = ScrollOffset + 1;
  SetState();
}


////////////////////////////////////////////////////////////////////////////////
on "save"
{
  if(SelectedSlot >= 0)
  {
    var WinName = Game.LoadWindow("interface\system\neosavename.window");
    if(Game.IsSaveSlotUsed(SelectedSlot))
    {
      WinName.xDescription = Game.GetSaveSlotDescription(SelectedSlot);
    }

    WinName.Center();
    WinName.GoSystemExclusive();
    if(WinName.xResult==true)
    {
      self.xResult = true;
      Game.SaveGame(SelectedSlot, WinName.xDescription);
    }
    else
      self.xResult = false;

    Game.UnloadObject(WinName);

    if(self.xResult) self.Close();
  }
}


////////////////////////////////////////////////////////////////////////////////
on "Keypress"
{
  var button;

  if(Keyboard.KeyCode==VK_ESCAPE){
    button = self.GetWidget("close");
    button.Press();
  }
}

////////////////////////////////////////////////////////////////////////////////
function SetState()
{
  var BtnUp = self.GetWidget("up");
  var BtnDown = self.GetWidget("down");
  var BtnSave = self.GetWidget("save");

  //var Thumbnail = self.GetControl("thumbnail");

  BtnUp.Disabled = (ScrollOffset <= 0);
  BtnDown.Disabled = (ScrollOffset+NumSlotButtons >= NumSavegames);
  BtnSave.Disabled = (SelectedSlot < 0);

  for(var i=0; i<NumSlotButtons; i=i+1)
  {
    var SaveSlot = ScrollOffset + i;
    var SlotButton = self.GetWidget(i+1);

    SlotButton.Pressed = (SaveSlot==SelectedSlot);
    SlotButton.Text = SaveSlot;
    if(Game.IsSaveSlotUsed(SaveSlot))
      SlotButton.Text = Game.GetSaveSlotDescription(SaveSlot);
    else
      SlotButton.Text = Game.ExpandString("/INTERF0015/< empty >");
  }

  //if(Game.IsSaveSlotUsed(SelectedSlot)) Thumbnail.SetImage("savegame:" + SelectedSlot);
  //else Thumbnail.SetImage(null);
}

////////////////////////////////////////////////////////////////////////////////
on "1"
{
  SelectedSlot = ScrollOffset;
  SetState();
}
on "2"
{
  SelectedSlot = ScrollOffset+1;
  SetState();
}
on "3"
{
  SelectedSlot = ScrollOffset+2;
  SetState();
}
on "4"
{
  SelectedSlot = ScrollOffset+3;
  SetState();
}
on "5"
{
  SelectedSlot = ScrollOffset+4;
  SetState();
}
on "6"
{
  SelectedSlot = ScrollOffset+5;
  SetState();
}
